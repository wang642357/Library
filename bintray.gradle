apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
boolean isHasFile = false
if (project.rootProject.findProject('local.properties') != null) {
    isHasFile = true
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

def gitUrl = 'git@github.com:wang642357/JsLibrary.git'   // Git仓库的url
def siteUrl = 'git@github.com:wang642357/JsLibrary'   // 项目的主页

version = rootProject.ext.android["versionName"]
group = "com.wjx"


bintray {
    user = isHasFile ? properties.getProperty("bintray.user") : System.getenv("bintray_user")
    key = isHasFile ? properties.getProperty("bintray.apikey") : System.getenv("bintray_apikey")

    pkg {
        repo = 'maven'
        name = "Library"
        licenses = ["Apache-2.0"]
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = true // 是否是公开项目。

        version {
            name = rootProject.ext.android["versionName"]
            desc = 'A common development library based on Google AAC architecture and MVVM design pattern.'
            released = new Date()
            vcsTag = 'v' + rootProject.ext.android["versionName"]
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name "Library"
                description 'A common development library based on Google AAC architecture and MVVM design pattern.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wang642357'        //填写bintray或者github的用户名
                        name 'wangjianxiong'         //姓名，可以是中文
                        email 'wang642357@foxmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
